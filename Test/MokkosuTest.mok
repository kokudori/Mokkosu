#------------------------------------------------------------
#! @file    MokkosuTest.mok
#! @brief   Mokkosu動作確認用プログラム (言語 + コアライブラリ)
#! @author  lambdataro
#------------------------------------------------------------

do println "====================";
do println "   Mokkosu Test";
do println "====================";

#include "List.mok";
#include "Print.mok";
#include "Graphics.mok";

let assert expr =
  if expr -> ()
  else error "assert";

#----------------------------------------
# コメント
#----------------------------------------

#[
let x = false;
#]

##[
let x = true;
#]
assert x;

println "コメント: OK";

#----------------------------------------
# 識別子
#----------------------------------------

let __test_variable = true;
let variable123 = true;

println "識別子: OK";

#----------------------------------------
# let文
#----------------------------------------

let x = 123;
assert <| x == 123;

let f x y = x + y;
assert <| f 10 20 == 30;

let f x = x;
assert <| f f 10 == 10;

println "let文: OK";

#----------------------------------------
# fun文
#----------------------------------------

fun f x = x * 2;
assert <| f 10 == 20;

fun f x y = x + y;
assert <| f 10 20 == 30;

fun fact n =
  if n == 0 -> 1
  else n * fact (n - 1);
assert <| fact 5 == 120;

fun fact = {
  0 -> 1;
  n -> n * fact (n - 1);
};
assert <| fact 5 == 120;

fun even = { 0 -> true;  n -> odd (n - 1)  }
and odd  = { 0 -> false; n -> even (n - 1) };
assert <| even 10 == true;
assert <| even 11 == false;
assert <| odd 10 == false;
assert <| odd 11 == true;

println "fun文: OK";

#----------------------------------------
# type文
#----------------------------------------

# nat

type nat = zero | succ(nat);

fun addnat x y =
  match x {
    ~zero -> y;
    ~succ(n) -> succ(addnat n y);
  };

fun eqnat x y =
  match (x, y) {
    (~zero, ~zero) -> true;
    (~zero, ~succ(_)) -> false;
    (~succ(_), ~zero) -> false;
    (~succ(n), ~succ(m)) -> eqnat n m;
  };

fun int_to_nat n =
  if n == 0 -> zero
  else succ(int_to_nat (n - 1));

fun nat_to_int = {
  ~zero -> 0;
  ~succ(n) -> 1 + nat_to_int n;
};

assert <| nat_to_int
  (addnat (int_to_nat 10) (int_to_nat 20)) == 30;

# dict

type Dict<K,V> = Empty | Entry(K, V, Dict<K,V>);

fun list_to_dict = {
  [] -> Empty;
  (k,v) :: rest -> Entry(k, v, list_to_dict rest);
};

fun lookup_dict k = {
  ~Empty -> error <| "Not found: " ^ to_string k;
  ~Entry(k2, v, rest) ? k == k2 -> v;
  ~Entry(k2, v, rest) -> lookup_dict k rest;
};

assert <| lookup_dict "b" (
  list_to_dict [("a", 123), ("b", 234), ("c", 345)]) == 234;
  
# lambda

type Expr = EInt(Int)
          | EAdd(Expr, Expr)
          | EMul(Expr, Expr)
          | EVar(String)
          | EFun(String, Expr)
          | EApp(Expr, Expr);

type Value = VInt(Int)
           | VFun(String, Expr, Env)
and Env = Env([(String, Value)]);

fun eval env = {
  ~EInt(n) -> VInt(n);
  ~EAdd(e1, e2) ->
    let ~VInt(n1) = eval env e1 in
    let ~VInt(n2) = eval env e2 in
    VInt(n1 + n2);
  ~EMul(e1, e2) ->
    let ~VInt(n1) = eval env e1 in
    let ~VInt(n2) = eval env e2 in
    VInt(n1 * n2);
  ~EVar(x) -> lookup_env x env;
  ~EFun(x, e) -> VFun(x, e, env);
  ~EApp(e1, e2) ->
    let ~VFun(x, body, fenv) = eval env e1 in
    let arg = eval env e2 in
    let fenv2 = extenv x arg fenv in
    eval fenv2 body
}
and lookup_env x ~Env(env) = from_just (lookup x env)
and extenv x arg ~Env(env) = Env((x, arg) :: env);

let test =
  EApp (EApp (EFun ("f", EFun("x",
    EApp(EVar "f", EApp (EVar "f", EVar "x")))),
      EFun("x", EMul(EVar "x", EInt 2))), EInt 10);

let ~VInt(result) = eval (Env []) test;

assert <| result == 40;

println "type文: OK";

#----------------------------------------
# 整数型
#----------------------------------------

let x = 123;
assert <| 0xff == 255;
assert <| 0XFF == 255;
assert <| 0o33 == 27;
assert <| 0O33 == 27;
assert <| 0b1100 == 12;
assert <| 0B1100 == 12;

println "整数型: OK";

#----------------------------------------
# 浮動小数点数型
#----------------------------------------

let x = 3.;
let x = 3.14;
let x = 3.14e3;
let x = 3.14e-3;
let x = 3.14e+3;
let x = 314e21;
let x = 3.14e21;
let x = 314E21;
let x = 3.14E21;
let x = 314e-21;
let x = 314E-21;
let x = 3.14e-21;
let x = 3.14E-21;

println "浮動小数点数型: OK";

#----------------------------------------
# 文字列型
#----------------------------------------

let s = "";
let s = "abc";
let s = @"abc";
let s = @"abc\";
let s = "\'\"\\\a\b\f\n\r\t\v";
let s = "\0";
let s = "\u0066";
assert <| "abc\\def" == @"abc\def";

println "文字列型: OK";

#----------------------------------------
# 文字型
#----------------------------------------

let c = 'A';
let c = 'あ';
let c = '\"';
let c = '\u0066';

println "文字型: OK";

#----------------------------------------
# 真偽値型
#----------------------------------------

let b = true;
let b = false;

println "真偽値型: OK";

#----------------------------------------
# ユニット型
#----------------------------------------

let x = ();

println "ユニット型: OK";

#----------------------------------------
# リスト型
#----------------------------------------

let x = [];
let x = [1,2,3];

println "リスト型: OK";

#----------------------------------------
# タプル型
#----------------------------------------

let x = (1, "abc");
let x = (1, 2, 3);

println "タプル型: OK";

#----------------------------------------
# 関数型
#----------------------------------------

let f = \x -> \y -> x + y;
assert <| f 10 20 == 30;

let f = \x y -> x + y;
assert <| f 10 20 == 30;

let f = \x -> x;
assert <| f 6 == 6;

println "関数型: OK";

#----------------------------------------
# do式
#----------------------------------------

do do print "abc"; println "def" in ();
do do print "abc"; println "def"; in ();
do do print "abc"; println "def" end;
do do print "abc"; println "def"; end;

println "do式: OK";

#----------------------------------------
# let式
#----------------------------------------

do let x = 12 in assert <| (x * x) == 144;
do let f x = x * x in assert <| (f 12) == 144;
do let f x y = x * y in assert <| (f 5 6) == 30;

println "let式: OK";

#----------------------------------------
# fun式
#----------------------------------------

do fun f x = x * 2 in
   assert <| f 10 == 20;

do fun f x y = x + y in
   assert <| f 10 20 == 30;

do fun fact n =
     if n == 0 -> 1
     else n * fact (n - 1)
   in assert <| fact 5 == 120;

do fun fact = {
     0 -> 1;
     n -> n * fact (n - 1);
   } in
   assert <| fact 5 == 120;

do fun evan = { 0 -> true;  n -> odd (n - 1)  }
   and odd  = { 0 -> false; n -> even (n - 1) }
   in do
     assert <| even 10 == true;
     assert <| even 11 == false;
     assert <| odd 10 == false;
     assert <| odd 11 == true;
   end;

println "fun式: OK";

#----------------------------------------
# if式
#----------------------------------------

assert <| (if true -> 123 else 456) == 123;
assert <| (if false -> 123 else 456) == 456;

println "if式: OK";

#----------------------------------------
# pat式
#----------------------------------------

assert <| (pat [] = [] -> 123 else 345) == 123;
assert <| (pat [] = [1] -> 123 else 345) == 345;
assert <| (pat [x] ? x == 1 = [1] -> 123 else 345) == 123;
assert <| (pat [x] ? x == 2 = [1] -> 123 else 345) == 345;

println "pat式: OK";

#----------------------------------------
# ブロック式
#----------------------------------------

fun sum = {
  [] -> 0;
  x :: xs -> x + sum xs;
};

assert <| sum [1,2,3] == 6;

assert <|
  (let x = 12 in
  { 0 -> x; let x = 13; _ -> x } 0) == 12;

assert <|
  (let x = 12 in
  { 0 -> x; let x = 13; _ -> x } 1) == 13;

assert <| ({ fun f x = x + 2; x -> f x; } 10) == 12;

do { do print "hello, "; do println "world."; _ -> () } 10;

println "ブロック式: OK";

#----------------------------------------
# for式
#----------------------------------------

assert <|
  (sum (for x <- 1 .. 5 in x * x)) == 55;

assert <|
  (sum (for x <- 1 .. 5; y <- 1 .. 5 in x * y)) == 225;

assert <|
  (sum (for x <- 1 .. 10; if x % 2 == 0 in x * x)) == 220;

println "for式: OK";

#----------------------------------------
# 型強制式
#----------------------------------------

fun f (x : Int) = x;

println "型強制式: OK";

#----------------------------------------
# コンス式
#----------------------------------------

assert <| sum (1 :: 2 :: 3 :: []) == 6;

println "コンス式: OK";
